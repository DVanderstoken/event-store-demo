buildscript {

    ext {

        springBootVersion = '2.0.1.RELEASE'
        springCloudVersion = 'Finchley.BUILD-SNAPSHOT'
        springCloudContractVersion = '2.0.0.M8'

    }

    repositories {

        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }

    }

    dependencies {

        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath "org.springframework.cloud:spring-cloud-contract-gradle-plugin:${springCloudContractVersion}"

    }

}

apply plugin: 'idea'
apply plugin: 'eclipse'

group 'io.pivotal.dmfrey'
version '1.0-SNAPSHOT'

subprojects {

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'groovy'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'spring-cloud-contract'
    apply from: "$rootDir/tools/jacoco.gradle"

    group = 'io.pivotal.dmfrey'
    version = '0.0.1-SNAPSHOT'
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    repositories {

        mavenCentral()
        maven { url "https://repo.spring.io/snapshot" }
        maven { url "https://repo.spring.io/milestone" }

    }

    ext {

        springCloudVersion = 'Finchley.BUILD-SNAPSHOT'

    }

    dependencies {

        // Spring dependencies


        // Spring Boot dependencies
        compile('org.springframework.boot:spring-boot-starter-actuator')
        compile('org.springframework.boot:spring-boot-starter-web')


        // Spring Cloud dependencies
        compile('org.springframework.cloud:spring-cloud-starter-netflix-eureka-client')


        // Third-party dependencies
        compile('org.projectlombok:lombok:1.16.18')
        compile('io.vavr:vavr:0.9.2')


        // Test dependencies
        testCompile('org.springframework.boot:spring-boot-starter-test')

        testCompile('org.codehaus.groovy:groovy-all:2.4.6')
        testCompile('org.spockframework:spock-core:1.0-groovy-2.4')
        testCompile('org.spockframework:spock-spring:1.0-groovy-2.4')
        testCompile('org.springframework.cloud:spring-cloud-starter-contract-verifier')
        testCompile('org.springframework.cloud:spring-cloud-contract-wiremock')

    }

    dependencyManagement {

        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-contract-dependencies:${springCloudContractVersion}"
        }

    }

    contracts {

        basePackageForTests = 'io.pivotal.dmfrey.eventStoreDemo'
        baseClassForTests = 'io.pivotal.dmfrey.eventStoreDemo.ContractBaseTests'

    }

    tasks.withType (Test) {

        maxParallelForks = 2

        testLogging {

            showStandardStreams = true
            events 'started', 'passed', 'failed'

        }

        jacoco {

            append = false
            destinationFile = file("$buildDir/jacoco/${name}.exec")
            classDumpDir = file("$buildDir/jacoco/${name}_ClassDump")

        }

    }

    test {

        description 'Used for running unit tests.'

        include '**/**Tests.java'

    }

    build.finalizedBy jacocoTestReport, jacocoTestCoverageVerification

}
